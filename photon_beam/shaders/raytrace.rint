
#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable

#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_buffer_reference2 : require

#include "gltf.glsl"
#include "raycommon.glsl"
#include "sampling.glsl"
#include "host_device.h"

hitAttributeEXT vec3 beamHit;

layout(std430, set = 0, binding = 2) readonly buffer PhotonBeams{

    uint subBeamCount;
    uint beamCount;
    uint _padding_beams[2];
	PhotonBeam beams[];
};

layout(push_constant) uniform _PushConstantRay { PushConstantRay pcRay; };
// clang-format on


void main()
{
    vec3 rayOrigin    = gl_WorldRayOriginEXT;
    vec3 rayDirection = gl_WorldRayDirectionEXT;
    const vec3 rayEnd = rayOrigin + rayDirection * gl_RayTmaxEXT;
    float rayLength = gl_RayTmaxEXT - 0.0001;

    PhotonBeam beam = beams[gl_InstanceCustomIndexEXT];

    vec3 beamDirection = normalize(beam.endPos - beam.startPos);
    float beamLength = length(beam.endPos - beam.startPos);
    const vec3 rayBeamCross = cross(rayDirection, beamDirection);
    vec3 norm1 = cross(rayDirection, rayBeamCross);
    vec3 norm2 = cross(beamDirection, rayBeamCross); 

    // if ray and the beam is parallel or almost prallel
    // make ray meet beam at the furthest point from the ray start point.
    if (abs(dot(beamDirection, rayDirection)) < 0.0001){
        vec3 rayPoint = rayEnd;
        float beamPointAt =  dot(rayPoint - beam.startPos, beamDirection);
        beamPointAt = min(beamPointAt, beamLength);
        beamPointAt = max(beamPointAt, 0);
        vec3 beamPoint = beam.startPos + beamDirection * beamPointAt;
        float rayPointAt = dot(beamPoint - rayOrigin, rayDirection);
        rayPointAt = min(rayLength, rayPointAt);
        rayPointAt = max(0.0, rayPointAt);
        rayPoint = rayOrigin + rayDirection * rayPointAt;

        if(length(beamPoint - rayPoint) >  pcRay.beamRadius)
            return;

        beamHit = beamPoint;
        reportIntersectionEXT(rayPointAt, 0);
        return;
    }


    // get the nearest points between camera ray and beam
    vec3 rayPoint = rayOrigin + dot(beam.startPos - rayOrigin, norm2) / dot(rayDirection, norm2) * rayDirection;
    vec3 beamPoint = beam.startPos + dot(rayOrigin - beam.startPos, norm1) / dot(beamDirection, norm1) * beamDirection;

    float rayPointAt = dot(rayPoint - rayOrigin, rayDirection); 
    float beamPointAt = dot(beamPoint - beam.startPos, beamDirection);

    if(beamPointAt < 0)
    {
        beamPoint = beam.startPos;
        beamPointAt = 0;
        rayPointAt =  dot(beamPoint - rayOrigin, beamDirection) / dot(beamDirection, rayDirection);
        rayPoint = rayOrigin + rayDirection * rayPointAt;
    }
    else if(beamPointAt > beamLength)
    {
        beamPoint = beam.endPos;
        beamPointAt = beamLength;
        rayPointAt = dot(beamPoint - rayOrigin, beamDirection) / dot(beamDirection, rayDirection);
        rayPoint = rayOrigin + rayDirection * rayPointAt;
    }
  
    if(rayPointAt < 0)
    {
        rayPoint = rayOrigin;
        beamPointAt = dot(rayPoint - beam.startPos, beamDirection);
        beamPoint = beam.startPos + beamDirection * beamPointAt;
    }
    else if(rayPointAt > rayLength)
    {
        rayPoint = rayEnd;
        beamPointAt = dot(rayPoint - beam.startPos, beamDirection);
        beamPoint = beam.startPos + beamDirection * beamPointAt;
    }


    // return none if the distance between the nearest points is longer than the beam radius
    if(length(beamPoint - rayPoint) >  pcRay.beamRadius)
    {
        //reportIntersectionEXT(-1, 0);
        return;
    }
    

    float beamBoxStartAt = length(vec3(gl_ObjectToWorldEXT * vec4(0.0,0.0,0.0, 1.0)) - beam.startPos);

    // return none if this subbeam does not contain the beam point
    if(beamPointAt < beamBoxStartAt || beamBoxStartAt + pcRay.beamRadius * 2 <= beamPointAt)
    {
        //reportIntersectionEXT(-1, 0);
        return;
    }

    beamHit = beamPoint;
  

    rayPointAt = length(rayPoint - rayOrigin);
    reportIntersectionEXT(rayPointAt, 0);
}
